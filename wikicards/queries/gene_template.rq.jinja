SELECT
?gene ?geneLabel ?UniProt_protein_ID 
?protein ?proteinLabel ?protein_length ?protein_mass
(REPLACE(STR(?article), "https://en.wikipedia.org/wiki/", "") as ?en_wiki_label)
(GROUP_CONCAT(DISTINCT ?geneAltLabel_; SEPARATOR=" | ") as ?geneAltLabel)

?cytogenetic_location
?genomic_start
?genomic_end
((xsd:integer(?genomic_end) - xsd:integer(?genomic_start)) AS ?genomic_length)
?chromosome_number 

# External ids
?Entrez_Gene_ID
?MeSH_descriptor_ID

(GROUP_CONCAT(DISTINCT ?NCBI_locus_tag_; SEPARATOR=", ") as ?NCBI_locus_tag)
(GROUP_CONCAT(DISTINCT ?MGI_Gene_Symbol_; SEPARATOR=", ") as ?MGI_Gene_Symbol)
(GROUP_CONCAT(DISTINCT ?HGNC_gene_symbol_; SEPARATOR=", ") as ?HGNC_gene_symbol)
(GROUP_CONCAT(DISTINCT ?HGNC_ID_; SEPARATOR=", ") as ?HGNC_ID)
(GROUP_CONCAT(DISTINCT ?OMIM_ID_; SEPARATOR=", ") as ?OMIM_ID)
(GROUP_CONCAT(DISTINCT ?Ensembl_gene_ID_; SEPARATOR=", ") as ?Ensembl_gene_ID)
(GROUP_CONCAT(DISTINCT ?RefSeq_RNA_ID_; SEPARATOR=", ") as ?RefSeq_RNA_ID)
(GROUP_CONCAT(DISTINCT ?PharmGKB_ID_; SEPARATOR=", ") as ?PharmGKB_ID)
(GROUP_CONCAT(DISTINCT ?Ensembl_transcript_ID_; SEPARATOR=", ") as ?Ensembl_transcript_ID)
WHERE {

    ?gene wdt:P353 "{{target}}" .    
    ?gene wdt:P688 ?protein . 
    ?gene skos:altLabel ?geneAltLabel_ . 
    FILTER ( LANG( ?geneAltLabel_) = "en")


    OPTIONAL {
      ?protein wdt:P2043 ?protein_length . 
      ?protein wdt:P2067 ?protein_mass . 
    }
    OPTIONAL{
    ?article schema:about ?protein .
    ?article schema:inLanguage "en" .
    FILTER (SUBSTR(str(?article), 1, 25) = "https://en.wikipedia.org/")

    }


    OPTIONAL {?gene wdt:P4196 ?cytogenetic_location}

    OPTIONAL {?gene p:P644 [ps:P644 ?genomic_start;
                            pq:P659 wd:Q20966585  ].
    }
    OPTIONAL {?gene p:P645 [ps:P645 ?genomic_end;
                            pq:P659 wd:Q20966585 ] .
    }

    OPTIONAL {?gene p:P1057 [ps:P1057 ?chromosome;
                            pq:P659 wd:Q20966585 ] .
              ?chromosome wdt:P1813 ?chromosome_number .
    }



# IDs
    OPTIONAL { ?gene wdt:P2393 ?NCBI_locus_tag_ . } .    
    OPTIONAL { ?gene wdt:P2394 ?MGI_Gene_Symbol_ . } .    
    OPTIONAL { ?gene wdt:P353 ?HGNC_gene_symbol_ . } .    
    OPTIONAL { ?gene wdt:P354 ?HGNC_ID_ . } .    
    OPTIONAL { ?gene wdt:P492 ?OMIM_ID_ . } .    
    OPTIONAL { ?gene wdt:P594 ?Ensembl_gene_ID_ . } .    
    OPTIONAL { ?gene wdt:P639 ?RefSeq_RNA_ID_ . } .    
    OPTIONAL { ?gene wdt:P7001 ?PharmGKB_ID_ . } .    
    OPTIONAL { ?gene wdt:P704 ?Ensembl_transcript_ID_ . } .    


    OPTIONAL { ?protein wdt:P352 ?UniProt_protein_ID . } .  
    OPTIONAL { ?gene wdt:P351 ?Entrez_Gene_ID . } .    
    OPTIONAL { ?gene wdt:P486 ?MeSH_descriptor_ID . } .
SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
   
}
GROUP BY ?gene ?geneLabel ?UniProt_protein_ID ?Entrez_Gene_ID ?MeSH_descriptor_ID ?article ?en_wiki_label ?cytogenetic_location
?genomic_start
?genomic_end
?chromosome_number 
?protein ?proteinLabel ?protein_length ?protein_mass
